plugins {
    id 'idea'
    id 'maven-publish'
    id 'groovy-gradle-plugin'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'io.github.groovymc.simpleci' version '0.1.+'
}

project.ext.set('gradle.publish.key', System.getenv('PLUGIN_PORTAL_KEY') ?: findProperty('gradle.publish.key'))
project.ext.set('gradle.publish.secret=', System.getenv('PLUGIN_PORTAL_SECRET') ?: findProperty('gradle.publish.secret'))

final startTag = '1.1'
versioning {
    fromTag.set(startTag)
}
changelog {
    start.set(startTag)
}

println("Version: ${project.version = versioning.calculatedVersion}")

group = 'com.matyrobbrt.gradle.jarinjar'
archivesBaseName = 'jarinjar'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.maven:maven-artifact:3.8.5'
}

pluginBundle {
    tags = ['minecraft', 'jar-in-jar']
    vcsUrl = 'https://github.com/Matyrobbrt/GradleJarInJar'
    website = 'https://github.com/Matyrobbrt/GradleJarInJar'
}

gradlePlugin {
    plugins {
        forgejij {
            id = 'com.matyrobbrt.jarinjar'
            displayName = 'JarInJar'
            description = 'A Gradle plugin for including jars in jars'
            implementationClass = 'com.matyrobbrt.gradle.jarinjar.JarInJarPlugin'
        }
    }
}

license {
    header = rootProject.file('licenseheader.txt')
}

java {
    withSourcesJar()
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

project.afterEvaluate {
    publishing {
        publications {
            named('pluginMaven').configure {
                changelog.addArtifact(it)
            }
        }
    }
}